group = "com.mobius.tim"
version = "1.0-SNAPSHOT"

def timFrbVersion = project.findProperty("timFrbVersion") ?: "0.0.1"
def timFrbArchiveName = "tim_frb-artifacts-v${timFrbVersion}.zip"
def timFrbDownloadUrl = "https://github.com/mobius-toy/tim_artifacts/releases/download/v${timFrbVersion}/${timFrbArchiveName}"
def timFrbWorkDir = new File(buildDir, "timFrb")
def timFrbArchive = new File(timFrbWorkDir, timFrbArchiveName)
def timFrbJniLibsDir = new File(timFrbWorkDir, "jniLibs")

buildscript {
    ext.kotlin_version = "2.1.0"
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.9.1")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

android {
    namespace = "com.mobius.tim"

    compileSdk = 36

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
        main.jniLibs.srcDir timFrbJniLibsDir
    }

    defaultConfig {
        minSdk = 24
    }

    dependencies {
        testImplementation("org.jetbrains.kotlin:kotlin-test")
        testImplementation("org.mockito:mockito-core:5.0.0")
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }
}

tasks.register("fetchTimFrbArtifacts") {
    outputs.dir timFrbJniLibsDir
    doLast {
        timFrbWorkDir.mkdirs()

        if (!timFrbArchive.exists()) {
            logger.lifecycle("Downloading tim_frb artifacts ${timFrbArchiveName} from ${timFrbDownloadUrl}")
            ant.get(src: timFrbDownloadUrl, dest: timFrbArchive)
        }

        delete timFrbJniLibsDir
        copy {
            from(zipTree(timFrbArchive)) {
                include "android/**"
            }
            into timFrbJniLibsDir
            includeEmptyDirs = false
            eachFile { details ->
                details.path = details.path.replaceFirst("android/", "")
            }
        }
    }
}

tasks.matching { it.name == "preBuild" }.configureEach {
    dependsOn("fetchTimFrbArtifacts")
}
